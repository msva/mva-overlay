# Copyright 1999-2020 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=7

VCS="git"
GITHUB_A="luvit"
CMAKE_MAKEFILE_GENERATOR="emake"

inherit patches cmake-utils lua

DESCRIPTION="libuv bindings for lua (jit & 5.x)"
HOMEPAGE="https://github.com/luvit/luv"

LICENSE="Apache-2.0"
SLOT="0"
KEYWORDS=""
IUSE="examples"

EXAMPLES=( examples/. )

RDEPEND="dev-libs/libuv"
EGIT_SUBMODULES=( '*' -libuv -luajit -lua )

all_lua_prepare() {
	sed -r \
		-e "1iset\(CMAKE_INSTALL_RPATH \"\"\)" \
		-e "s@/lib([/\"'])@/$(get_libdir)\1@" \
		-i CMakeLists.txt
	rm Makefile # we don't need default one; It'll be regenerated by cmake when needed.
	patches_src_prepare
	cmake-utils_src_prepare
}

each_lua_configure() {
	local mycmakeargs=(
		-DBUILD_MODULE=ON
		-DBUILD_SHARED_LIBS=ON
		-DWITH_SHARED_LIBUV=ON
		-DLUA_BUILD_TYPE="System"
		-DLUA=$(lua_get_lua)
		-DLUA_LIBDIR=$(lua_get_cmoddir)
		-DLUA_INCDIR=$(lua_get_pkgvar includedir)
		-DLIBDIR=$(lua_get_cmoddir)
		-DLUADIR=$(lua_get_lmoddir)
		-DWITH_LUA_ENGINE="$(lua_is_jit && echo 'LuaJIT' || echo 'Lua')"
		# ^ TODO: use-flags
	)
	cmake-utils_src_configure
}

all_lua_configure() {
	local lua_impl="$(lua_get_implementation)"
	lua_impl="${lua_impl//lua/Lua}"
	lua_impl="${lua_impl//jit/JIT}"
	local mycmakeargs=(
		-DBUILD_MODULE=OFF
		-DBUILD_SHARED_LIBS=ON
		-DWITH_SHARED_LIBUV=ON
		-DLUA_BUILD_TYPE="System"
		-DWITH_LUA_ENGINE="${lua_impl}"
		# ^ TODO: use-flags
	)
	cmake-utils_src_configure
}

all_lua_compile() {
	cmake-utils_src_compile
}

all_lua_install() {
	cmake-utils_src_install
}

pkg_postinst() {
	einfo "Be aware of the fact, that you'll need to rebuild ${CATEGORY}/${PN}"
	einfo "if you'll decide to switch default lua implementation to another value"
}
